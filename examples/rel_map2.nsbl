graph g1;
string file = "rel_map.xml";
file >> g1;

//print << g1;

//print_graph(g1);
//get Cindy's friend
print << "1> find all Cindy's friend\n\t";
vlist vl_Cindy_friends = g1.allV?[@n=="Cindy"]|outE?[@rel=="friend"]|endV;
print_vlist(vl_Cindy_friends);

//get Cindy's friend's friends
print << "2> find all Cindy's friend's friends\n\t";
vlist vl_Cindy_friends_friends = g1.allV?[@n=="Cindy"]|outE?[@rel=="friend"]|endV|outE?[@rel=="friend"]|endV?[@n!="Cindy"];
print_vlist(vl_Cindy_friends_friends);

print << "3> find all Cindy's friend's friends, who is female or age > 23\n\t";
vlist vl_Cindy_friends_friends_2 = g1.allV?[@n=="Cindy"]|outE?[@rel=="friend"]|endV|outE?[@rel=="friend"]|endV?[@n!="Cindy"&&(@g=="f"||@age>23)];
print_vlist(vl_Cindy_friends_friends_2);

//get student's parent
print << "4> Uri's students' parents\n\t";
vlist vl_uri_student_parent = g1.allV?[@n=="Uri"]|outE?[@rel=="student"]|endV|outE?[@rel=="father"||@rel=="mother"]|endV;
print_vlist(vl_uri_student_parent);


int print_vlist(vlist l){
	foreach(vertex vt : l){
		print<<vt.n<<" : ";	
	}
	print<<"\n";
	return 0;
}

int print_graph(graph g){
	vlist vl1 = g.allV;
	print << "NODES IN GRAPH:\n";
	foreach(vertex vt: vl1){
		print <<vt.n<< " : ";	
	}
	print <<"\n---------------------\n";
	print <<"EDGES IN GRAPH:\n";
	elist el1 = g.allE;
	foreach(edge et: el1){
		vertex vs = get_edge_start(et);
		print <<"["<<vs.n<<"-->";
		vertex ve = get_edge_end(et);
		print <<ve.n<<"] | ";
	}
	return 0;
}

int follow(graph g, vertex follower, vertex followee ){
    edge e;
    e: follower -> followee;
    e.follow = true;
    e.level = 1;
    g<:e;
    return 0;
}

int set_level(vertex follower, vertex followee, int level){
    vlist vl1;
    vl1 <: follower;
    elist el1 = vl1|outE;
    foreach(edge e: el1){
        elist el2;
        el2<:e;
        vlist vl2 = el2|endV;
        foreach(vertex v : vl2){
            if(v==followee){
                e.level = level;
            }
        }
    }
    return 0;
}

edge get_edge(vertex follower, vertex followee){
    vlist vl1;
    vl1 <: follower;
    elist el1 = vl1|outE;
    foreach(edge e: el1){
        elist el2;
        el2<:e;
        vlist vl2 = el2|endV;
        foreach(vertex v : vl2){
            if(v==followee){
                e.isnull = false;
                return e;
            }
        }
    }
    edge en;
    en.isnull = true;
    return en;
}

vertex get_edge_start(edge e){
    elist el1;
    el1<:e;
    vlist vl = el1|strtV;
    if(length(vl)==1){
        vertex vt = vl[0];
        vt.isnull = false;
        return vt;
    }
    else{
        vertex vt;
        vt.isnull = true;
        return vt;
    }
}

vertex get_edge_end(edge e){
    elist el1;
    el1<:e;
    vlist vl = el1|endV;
    if(length(vl)==1){
        vertex vt = vl[0];
        vt.isnull = false;
        return vt;
    }
    else{
        vertex vt;
        vt.isnull = true;
        return vt;
    }
}

